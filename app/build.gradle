apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion sdkVersion.target

    defaultConfig {
        applicationId 'io.github.rsookram.mediaplayer'
        minSdkVersion sdkVersion.min
        targetSdkVersion sdkVersion.target
        versionCode 1
        versionName "1.0"

        resConfigs "en"
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-android.txt', 'proguard-rules.pro'

            // Just for testing release builds. Not actually distributed.
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude "/kotlin/**"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.allWarningsAsErrors = true
}

dependencies {
    implementation libraries.appCompat
    implementation libraries.constraintLayout

    implementation libraries.ktxCore

    implementation libraries.exoplayer
    implementation libraries.exoplayerUi
    implementation libraries.exoplayerHls
    implementation libraries.exoplayerMediaSession

    implementation libraries.kotlin
}

plugins {
    id 'com.android.application' version '8.9.0' apply true
}

repositories {
    google()
    mavenCentral()
}

android {
    namespace 'io.github.rsookram.mediaplayer'

    compileSdk 35

    defaultConfig {
        applicationId "io.github.rsookram.mediaplayer"
        minSdk 33
        targetSdk 35
        versionCode 1
        versionName "1.0"

        resourceConfigurations += ["en", "anydpi"]
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            if (System.getenv("STORE_FILE") != null) {
                storeFile file(System.getenv("STORE_FILE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    lint {
        checkReleaseBuilds = false
    }

    packaging.resources.excludes += [
        "/kotlin/**",
        "META-INF/*.version",
    ]
}

dependencies {
    def media = "1.5.1"
    implementation "androidx.media3:media3-exoplayer:$media"
    implementation "androidx.media3:media3-ui:$media"
}
